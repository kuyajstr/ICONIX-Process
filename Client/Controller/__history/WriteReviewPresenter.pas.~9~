unit WriteReviewPresenter;

interface

uses
  PresenterIntf,
  ViewIntf,
  Vcl.Forms,
  Json,
  CustomerReview,
  MVCFramework.RESTClient,
  Book;

type
  TWriteReviewPresenter = class(TInterfacedObject, IPresenter)
  private
    FView: IView;
    FCustomerReview: TCustomerReview;
    FBook: TBook;
    FRestClient: IMVCRESTClient;
    procedure SubmitReview(JSONBody: TJSONObject);
  public
    constructor Create;
    procedure DisplayView(ABook: TBook);
    procedure ValidateReview(Review: string; Rating: Integer);
  end;

implementation

uses
  WriteReviewView,
  Vcl.Dialogs,
  Vcl.Controls,
  AuthService;

constructor TWriteReviewPresenter.Create;
begin
  inherited Create;
  FRestClient := TMVCRESTClient.New.BaseURL('localhost', 8080);
  FRestClient.SetBearerAuthorization(TAuthService.GetInstance.GetToken);
  FView := TWriteReviewForm.Create(Self);
end;

procedure TWriteReviewPresenter.DisplayView(ABook: TBook);
begin
  var WriteReviewView := FView as TWriteReviewForm;
  FBook := ABook;
  WriteReviewView.BookTitleEdit.Text := FBook.Title;
  WriteReviewView.Show;
end;

procedure TWriteReviewPresenter.SubmitReview(JSONBody: TJSONObject);
begin
  var Resp := FRestClient.Post('/api/customer_reviews', JSONBody.ToString);
  if Resp.Success then
    ShowMessage('Review submitted! :)')
  else
    ShowMessage('Book not found');
end;

procedure TWriteReviewPresenter.ValidateReview(Review: string;
  Rating: Integer);
begin
  var JSONBody := TJSONObject.Create;
  JSONBody.AddPair('BookId', FBook.Id);
  JSONBody.AddPair('Review', Review);
  JSONBody.AddPair('Rating', Rating);

  var Resp := FRestClient.Post('/api/customer_reviews/validate', JSONBody.ToString);
  if Resp.Success then
  begin
    if MessageDlg('Submit Review?', mtConfirmation, [mbYes, mbNo], 0) = mrYes then
    begin
      SubmitReview(JSONBody);
      (FView as TForm).Close;
    end;
  end
  else
    ShowMessage(Resp.Content);
end;

end.
